plugins {
    id "org.jetbrains.kotlin.multiplatform"
}

apply plugin: "maven"
apply plugin: "maven-publish"


kotlin {
    if (include_js.toBoolean()) {
        js {
            browser {}
        }
    }
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin("reflect")
                implementation kotlin("stdlib")
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
            }
        }

        if (include_ios.toBoolean()){
            println "include_ios"
            targets {
                fromPreset(presets.iosArm64, 'iosArm64')
                fromPreset(presets.iosX64, 'iosX64')
                fromPreset(presets.iosSimulatorArm64, 'iosSimulatorArm64')
            }

            ios {
                binaries {
                    framework {
                        baseName = "d2v-$project.name"
                    }
                }
            }

            sourceSets {

                iosMain { dependsOn commonMain }

                configure([iosArm64Main, iosX64Main, iosSimulatorArm64Main]) {
                    dependsOn iosMain
                }

            }
        }

        if (include_js.toBoolean()) {
            jsMain {
                dependencies {
                    implementation kotlin("stdlib-js")
                }
            }
            jsTest {
                dependencies {
                    implementation kotlin("test-js")
                }
            }
        }
    }
}
